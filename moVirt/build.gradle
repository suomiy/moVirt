buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'me.tatarka:gradle-retrolambda:3.6.0'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
    mavenCentral()

    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

}

apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
ext.androidAnnottationVersion = '4.0.0'

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:multidex:1.0.1'

    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.blandware.android.atleap:core:0.4'
    compile 'org.springframework.android:spring-android-rest-template:2.0.0.M3'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.6.3'

    compile 'com.journeyapps:zxing-android-embedded:+@aar'
    compile 'com.google.zxing:core:3.+'

    apt "org.androidannotations:androidannotations:$androidAnnottationVersion"
    compile "org.androidannotations:androidannotations-api:$androidAnnottationVersion"
    compile "org.androidannotations:rest-spring-api:$androidAnnottationVersion"
    apt "org.androidannotations:rest-spring:$androidAnnottationVersion"

    compile 'com.github.bmelnychuk:atv:1.2.9'
    compile 'com.madgag.spongycastle:pkix:1.54.0.0'

    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'com.squareup.sqlbrite:sqlbrite:1.1.1'
    compile "com.github.akarnokd:rxjava2-interop:0.10.0"
}

def VERSION_MAJOR = 2
def VERSION_MINOR = 0
def VERSION_PATCH = 0
def VERSION_BUILD = 1

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "org.ovirt.mobile.movirt"
        minSdkVersion 16
        targetSdkVersion 25
        multiDexEnabled true

        versionCode 10
        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    apt {
        arguments {
            androidManifestFile variant.outputs[0].processResources.manifestFile
            resourcePackageName android.defaultConfig.applicationId
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LGPL2.1'
        // TODO: remove next line after SqlBrite has been ported to rxjava2 + remove interop library
        exclude 'META-INF/rxjava.properties'
    }



    buildTypes {
        release {
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-D"
        }
    }

    productFlavors {
        main {
        }

        alpha {
            applicationIdSuffix ".alpha"
        }

        beta {
            applicationIdSuffix ".beta"
        }
    }

    applicationVariants.all { variant ->
        def flavor = variant.productFlavors.get(0).name
        def build = variant.buildType.name

        def suffix = ""

        if (flavor != "main") {
            suffix = "." + flavor + "." + build
        }

        variant.resValue "string", "account_type", defaultConfig.applicationId + suffix
        variant.resValue "string", "content_authority", defaultConfig.applicationId + suffix + ".provider"


        def nameSuffix = ""
        def specialSuffix = ""

        if (flavor == "alpha") {
            nameSuffix = nameSuffix + " Alpha"
            specialSuffix = "a"
        }

        if (flavor == "beta") {
            nameSuffix = nameSuffix + " Beta"
            specialSuffix = "b"
        }

        if (build == "debug") {
            nameSuffix = nameSuffix + " Debug"
        }

        if (flavor != "main") {
            variant.mergedFlavor.versionName = "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}-${specialSuffix}${VERSION_BUILD}"
        }

        variant.resValue "string", "app_name", "moVirt" + nameSuffix
        variant.resValue "string", "moVirt_version", "App Version " + variant.mergedFlavor.versionName
    }

    // TODO: remove after fixing all android lint warnings
    lintOptions {
        abortOnError false
    }

}
